buildscript {
 repositories {
  jcenter()
  maven {
   url "https://plugins.gradle.org/m2/"
  }
 }
 dependencies {
  classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  classpath 'net.researchgate:gradle-release:2.2.2'
  classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
  classpath "gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.40"
  classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6'
 }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: "se.bjurr.gitchangelog.git-changelog-gradle-plugin"
apply plugin: 'com.github.sherter.google-java-format'

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
 filePath = "CHANGELOG.md";
 templateContent = new File('changelog.mustache').getText('UTF-8');
}

group = 'se.bjurr.violations'

dependencies {
 compile gradleApi()
}

sourceCompatibility = 1.7

repositories {
 mavenCentral()
 mavenLocal()
 jcenter()
 maven { url 'https://jitpack.io' }
}

dependencies {
 compile 'se.bjurr.violations:violation-comments-to-gitlab-lib:1.16'
}

eclipse {
 classpath {
  downloadSources = true
  downloadJavadoc = true
 }
}

pluginBundle {
  website = 'https://github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin'
  vcsUrl = 'https://github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin'
  description = 'Gradle plugin that will comment GitLab merge requests with results from static code analysis'
  tags = ['violation', 'static code analysis', 'Checkstyle', 'CPPLint', 'CPPCheck', 'CSSLint', 'Findbugs', 'Flake8', 'PyLint', 'Pep8', 'Mccabe', 'PyFlakes', 'JSHint', 'Lint', 'PerlCritic', 'PMD', 'ReSharper', 'XMLLint']

  plugins {
    violationsPlugin {
      id = 'se.bjurr.violations.violation-comments-to-gitlab-gradle-plugin'
      displayName = 'Violation Comments to GitLab Gradle Plugin'
    }
  }
}

modifyPom {
 project {
  name 'Violation Comments to GitLab Gradle Plugin'
  description 'Gradle plugin for Violation Comments to GitLab'
  url 'https://github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin'
  inceptionYear '2015'
   scm {
   url 'https://github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin'
   connection 'scm:https://tomasbjerre@github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin.git'
   developerConnection 'scm:git://github.com/tomasbjerre/violation-comments-to-gitlab-gradle-plugin.git'
  }

  licenses {
   license {
    name 'The Apache Software License, Version 2.0'
    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    distribution 'repo'
   }
  }

  developers {
   developer {
    id 'tomasbjerre'
    name 'Tomas Bjerre'
    email 'tomas.bjerre85@gmail.com'
   }
  }
 }
}

extraArchive {
 sources = true
 tests = true
 javadoc = true
}

compileJava.dependsOn 'googleJavaFormat'

afterReleaseBuild.dependsOn {
 [install, uploadArchives, publishPlugins]
}
